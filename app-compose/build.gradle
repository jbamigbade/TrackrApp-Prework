/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 31
    buildToolsVersion build_tools_version
    defaultConfig {
        applicationId 'com.example.android.trackr.compose'
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName '1.0.0-alpha01'
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        useIR = true
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion kotlin_version
    }
    testOptions {
        unitTests.all {
            systemProperty 'robolectric.enabledSdks', '28'
        }
        unitTests {
            includeAndroidResources = true
        }
    }
    packagingOptions {
        excludes += '/META-INF/AL2.0'
        excludes += '/META-INF/LGPL2.1'
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi',
        ]
    }
}

dependencies {
    implementation project(':shared')
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:2.1.5"

    implementation "androidx.core:core-ktx:1.16.0"
    implementation "androidx.compose.ui:ui:1.7.8"
    implementation "androidx.compose.material:material:1.7.8"
    implementation "androidx.compose.material:material-icons-extended:1.7.8"
    implementation "androidx.compose.ui:ui-tooling:1.7.8"
    implementation "androidx.activity:activity-compose:1.10.1"
    implementation "androidx.navigation:navigation-compose:2.8.9"
    implementation "com.google.accompanist:accompanist-insets-ui:0.36.0"
    implementation "com.google.accompanist:accompanist-flowlayout:0.36.0"

    implementation "com.google.dagger:hilt-android:2.56.1"
    implementation 'androidx.hilt:hilt-navigation-compose:1.2.0'
    kapt "androidx.hilt:hilt-compiler:1.2.0"
    kapt "com.google.dagger:hilt-android-compiler:2.56.1"

    testImplementation "junit:junit:4.13.2"
    testImplementation "com.google.truth:truth:1.4.4"
    testImplementation "androidx.test:core-ktx:1.6.1"
    testImplementation "androidx.test:runner:1.6.2"
    testImplementation "androidx.test.ext:junit:1.2.1"
    testImplementation "androidx.compose.ui:ui-test-junit4:1.7.8"
    testImplementation "androidx.arch.core:core-testing:2.2.0"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.10.2"
    testImplementation "org.robolectric:robolectric:4.14.1"

    // We need this for tests. It should be 'debugImplementation' to put the test manifest in the
    // app package.
    debugImplementation "androidx.compose.ui:ui-test-manifest:1.7.8"

    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation "com.google.truth:truth:1.4.4"
    androidTestImplementation "androidx.test:core-ktx:1.6.1"
    androidTestImplementation "androidx.test:runner:1.6.2"
    androidTestImplementation "androidx.test.ext:junit:1.2.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.7.8"
}
